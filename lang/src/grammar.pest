WHITESPACE = _{ (" " | "\t" | "\r" | "\n" ) }

String = ${ "\"" ~ Inner ~ "\"" }
Inner = @{ Char* }
Char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

// 42
Int = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

// 6.66
Float = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

Bool = { "true" | "false" }

SExpr = { "(" ~ Expr* ~ ")" }

Expr = { String | Float | Int | Bool | SExpr }

Program = { SOI ~ Expr* ~ EOI }
