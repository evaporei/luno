WHITESPACE = _{ (" " | "\t" | "\r" | "\n" ) }

// 6.66
Float = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

// 42
Int = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

Bool = { "true" | "false" }

Character = { "\\" ~ Char }

Keyword = @{ ":" ~ Symbol }

// Symbol = @{ (Letter ~ Char*?) | (ArithmeticOps | ComparisonOps) }
Symbol = @{ (Letter | ASCII_DIGIT | "_" | ArithmeticOps | ComparisonOps | "&")+ }
Letter = { 'a' .. 'z' | 'A' .. 'Z' }
ArithmeticOps = { "+" | "-" | "*" | "/" | "%" | "^" }
ComparisonOps = { "=" | "<" | ">" | "!" }

String = ${ "\"" ~ Inner ~ "\"" }
Inner = @{ Char* }
Char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

SExpr = { "(" ~ Expr* ~ ")" }

Expr = {
    Float
    | Int
    | Bool
    | Character
    | Keyword
    | Symbol
    | String
    | SExpr
}

Program = { SOI ~ Expr* ~ EOI }
